++++++++++++++++++++++++++++++++++++++++++++++++ 48
>,<[>-<-]>[>+>+<<-] считали первый символ; привели к числу и скопировали в две соседние ячейки
>>>>++++++++++ 10
[ умножаем на 10
	<<<[<+>-]
	>[<+>>+<-]
	>[<+>-]
	>-
]
<[-]<[-]<[-]<<++++++++++++++++++++++++++++++++++++++++++++++++>>,<<[>>-<<-]>>[<+>-]<[<+>-] в первой ячейке осталось введенно число; текущая ячейка вторая
,[-] любой символ разделитель
++++++++++++++++++++++++++++++++++++++++++++++++ 48
>,<[>-<-]>[>+>+<<-] считали первый символ; привели к числу и скопировали в две соседние ячейки
>>>>++++++++++ 10
[ умножаем на 10
	<<<[<+>-]
	>[<+>>+<-]
	>[<+>-]
	>-
]
<[-]<[-]<[-]<<++++++++++++++++++++++++++++++++++++++++++++++++>>,<<[>>-<<-]>>[<+>-]<[<+>-] во второй ячейке осталось введенное число; текущая ячейка третья
<[>>+>+<<<-]>>>[<<<+>>>-]<<< копируем ширину
<[>>+>>+<<<<-]>>>>[<<<<+>>>>-]<<<< копируем высоту
>>>[>>>+<<<-]<[>>>+<<<-]<[>>>+<<<-]<[>>>+<<<-]>>> сдвигаем блок с индексами вправо на три ячейки; текущая ячейка четвертая
>[-]<
><
[
	>>>[>+<-]>[<+<<+>>>-]<<<< в каждой строке считываем w (ширина) ячеек
	>[
		<<<< сдвигаемся влево на четыре; чтобы прочитать символ
		,---------- если это перевод строки; то считать следующий символ
		>+<
		[ не перевод строки
			--------------------------------- вычитаем дальше для проверки на плюс
			>[-]< сброс флага else
			[>>+<<-] выход из цикла if
		]
		>[ перевод строки
			,------------------------------------------- считали элемент матрицы; если значение плюс; то в ячейке есть клетка; иначе нет
			[>+<-] выход из цикла else
		]<
		>>[<<+>>-]<<
		>+< флаг else
		[ нет клетки
			>>[-]+++<< будущее значение ячейки = 3
			>[-]< сброс флага else
			[-] выход из цикла if
		]
		>[ есть клетка
			>[-]+< будущее значение ячейки = 1
			[-] выход из цикла else
		]<
		>>[<<+>>-]<< копируем в ячейку нужное значение
		>>>> в этой строке надо прочитать на ячейку меньше
		>>[>+<-]<[>+<-]<[>+<-]<[>+<-]>>- сдвиг блока вправо
	]<- надо прочитать на строку меньше
] ввод закончен
++++++++++.[-] печатаем перевод строки для красоты
>>>[<<<+<<+>>>>>-]<[<<<+<<+>>>>>-]<<<<< скопировали высоту и ширину в ячейки $1/$3 и $2/$4 соответсвенно
>[-]<
[ переместиться в начало матрицы
	>>>[>+<-]>[<+<<+>>>-]<<<< копируем; сколько ячеек в каждой строке
	>[
		<<[>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-] сдвиг ячейки матрицы и блока
		<<<<<<<<<<<<<- возвращаемся к счетчику
	]<-
]
>>>>>>>>,[ ожидание ввода enter'а; текущая ячейка $9
	---------- код enter'а = 10
	<<[-]+>> флаг else
	[ не enter
		<<[-]>> не выполнять else
		[-] выход из if
	]
	<<[ был enter; надо построить следующее поколение и вывести его
		<<<<<< переходим в начало блока
		!построение нового поколения
		>>[>>+<<-]>>[<<+<<+>>>>-]<<<< копируем высоту матрицы
		[
			>>>[>+<-]>[<+<<+>>>-]<<<< копируем ширину таблицы
			>[
				>>>>>>>>>>>>[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-] с первой по четырнадцатую ячейки блока сдвинуты вправо; текущая ячейка первая
				>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>-]<<<<<<<<<<<<<<< сдвинули ячейку; стоящую после блока данных
				>>>[>>>>>>>>>>+<<<<<<<<<<-]>>>>>>>>>>[<+<<<<<<<<<+>>>>>>>>>>-]<<<<<<<<<<<<[>>>>>>>>>>>->+<<<<<<<<<<<<-]>>>>>>>>>>>>[<<<<<<<<<<<<+>>>>>>>>>>>>-]<<<<<<<<<<<<< записали в $12 разность
				>>>>>>>[-]+>>>>>[ если $12 не ноль; то мы обрабатываем ячейку; находящуюся не на последней строке; текущая ячейка $12
					<<<<<<<<<<<< возвращаемся к клетке $0
					>>>>>>>[-]<<<<<<< обнуление флага else; возвращение в $0
					>>>>>>>>>>>>[-]<<<<<<<<<<<< обнуление $12
					нужно проверить; не на первой ли мы строке
					>[>>>>>>>>>>>+>+<<<<<<<<<<<<-]>>>>>>+>>>>>->[<<<<<<<<<<<<+>>>>>>>>>>>>-]<<<<<<<<<<<<< скопировали в $12 номер строки минус 1; вернулись в $0
					>>>>>>>>>>>>[ если в $12 не ноль; то мы не на последней строчке
						<<<<<<<<<<<< возвращаемся в $0
						>>>>[>>>>>>>>>+<<<<<<<<<-]>>>>>>>>[-]>[<+<<<<<<<<+>>>>>>>>>-]<<<<<<<<< << [>>>>>>>>>>>+<<<<<<<<<<<-]>>>>>>>>>>>[<-<<<<<<<<<<+>>>>>>>>>>>-]<<<<<<<<<<< << записываем в $12 разность ширины матрицы и индекса текущей ячейки в строке
						>>>>>>>[-]+<<<<<<< записали флаг else
						>>>>>>>>>>>>[ если в $12 не ноль; то мы не на левой крайней ячейке
							<<<<<<<<<<<< возвращаемся в $0
							>>>>>>>[-]+>>>>>[-]<<<<<<<<<<<< сбрасываем предыдущие значения; они будут нужны для другого условия
							>>[>>>>>>>>>>>+<<<<<<<<<<<-]>>>>>>>>>>>[<+<<<<<<<<<<+>>>>>>>>>>>-]<<<<<<<<<<<<<
							>>>>>>>>>>>>-[ если в $12 было больше единицы; то мы не в правой крайней ячейке
								<<<<<<<<<<<< текущая ячейка $0
								!обнуление счетчика живых соседей!
								>>>>>>>[-]>[-]>[-]<<<<<<<<<
								!средняя строка; не крайние ячейки; нужен обычный обход матрицы; текущая ячейка $0!
								!просмотр левого соседа!
								[-]+++
								!в $12 находится состояние клетки (жива/мертва); в $8 количество живых соседей!
								>>>>>>>>[-]>>>>[-] выход из if
							]<<<<<<<<<<<<
							>>>>>>>[ else; в $12 ноль; мы в правой крайней ячейке
								<<<<<<<
								!правая средняя ячейка матрицы; нужен особый обход матрицы!
								[-]+ оживить клетку
								>>>>>>>[-] выход из else
							]<<<<<<<
							>>>>>>>[-]<<<<<<< сбрасываем флаг else
							>>>>>>>>>>>>[-] возвращаемся в $12; выходим из if
						]<<<<<<<<<<<<
						>>>>>>>[ else; в $12 ноль; крайняя левая ячейка
							<<<<<<< возвращаемся в $0
							!левая средняя ячейка; нужен особый обход матрицы!
							[-]+ оживить клетку
							>>>>>>>[-] выход из else
						]<<<<<<<
						>>>>>>>>>>>>[-]<<<<<<<<<<<<
						>>>>>>>[-]<<<<<<< обнуляем флаг else
						>>>>>>>>>>>>[-] выходим из if
					]<<<<<<<<<<<<
					>>>>>>>[ в $12 ноль; мы на последней строке
						<<<<<<< возвращаемся в $0
						>>>>[>>>>>>>>>+<<<<<<<<<-]>>>>>>>>[-]>[<+<<<<<<<<+>>>>>>>>>-]<<<<<<<<< << [>>>>>>>>>>>+<<<<<<<<<<<-]>>>>>>>>>>>[<-<<<<<<<<<<+>>>>>>>>>>>-]<<<<<<<<<<< << записываем в $12 разность ширины матрицы и индекса текущей ячейки в строке
						>>>>>>>[-]+<<<<<<< записали флаг else
						>>>>>>>>>>>>[ если в $12 не ноль; то мы не на левой крайней ячейке
							<<<<<<<<<<<< возвращаемся в $0
							>>>>>>>[-]+>>>>>[-]<<<<<<<<<<<< сбрасываем предыдущие значения; они будут нужны для другого условия
							>>[>>>>>>>>>>>+<<<<<<<<<<<-]>>>>>>>>>>>[<+<<<<<<<<<<+>>>>>>>>>>>-]<<<<<<<<<<<<<
							>>>>>>>>>>>>-[ если в $12 было больше единицы; то мы не в правой крайней ячейке
								<<<<<<<<<<<<
								!нижняя строка; не крайние ячейки; нужен особый обход матрицы!
								[-]+ оживить клетку
								>>>>>>>[-]<<<<<<< обнуление флага else
								>>>>>>>>>>>>[-] выход из if
							]<<<<<<<<<<<<
							>>>>>>>[ else; в $12 ноль; мы в правой крайней ячейке
								<<<<<<<
								!правая нижняя ячейка матрицы; нужен особый обход матрицы!
								[-]+++ убить клетку
								>>>>>>>[-] выход из else
							]<<<<<<<
							>>>>>>>[-]<<<<<<< сбрасываем флаг else
							>>>>>>>>>>>>[-] возвращаемся в $12; выходим из if
						]<<<<<<<<<<<<
						>>>>>>>[ else; в $12 ноль; крайняя левая ячейка
							<<<<<<< возвращаемся в $0
							!левая нижняя ячейка; нужен особый обход матрицы!
							[-]+++ убить клeтку
							>>>>>>>[-] выход из else
						]<<<<<<<
						>>>>>>>>>>>>[-]<<<<<<<<<<<<
						>>>>>>>[-] выходим из else
					]<<<<<<<
					>>>>>>>[-]<<<<<<< обнуление флага else; возвращение в $0
					>>>>>>>>>>>>[-] выход из if
				]<<<<<<<<<<<< возвращаемся к $0
				>>>>>>>[ $12 ноль; мы на первой строке; текущая ячейка $7
					<<<<<<< возвращаемся к клетке $0
					>>>>[>>>>>>>>>+<<<<<<<<<-]>>>>>>>>[-]>[<+<<<<<<<<+>>>>>>>>>-]<<<<<<<<< << [>>>>>>>>>>>+<<<<<<<<<<<-]>>>>>>>>>>>[<-<<<<<<<<<<+>>>>>>>>>>>-]<<<<<<<<<<< << записываем в $12 разность ширины матрицы и индекса текущей ячейки в строке
					>>>>>>>[-]+<<<<<<< записали флаг else
					>>>>>>>>>>>>[ если в $12 не ноль; то мы не на левой крайней ячейке
						<<<<<<<<<<<< возвращаемся в $0
						>>>>>>>[-]+>>>>>[-]<<<<<<<<<<<< сбрасываем предыдущие значения; они будут нужны для другого условия
						>>[>>>>>>>>>>>+<<<<<<<<<<<-]>>>>>>>>>>>[<+<<<<<<<<<<+>>>>>>>>>>>-]<<<<<<<<<<<<<
						>>>>>>>>>>>>-[ если в $12 было больше единицы; то мы не в правой крайней ячейке
							<<<<<<<<<<<<
							!верхняя строка; не крайние ячейки; нужен особый обход матрицы!
							[-]+ оживить клeтку
							>>>>>>>[-]<<<<<<< обнуление флага else
							>>>>>>>>>>>>[-] выход из if
						]<<<<<<<<<<<<
						>>>>>>>[ else; в $12 ноль; мы в правой крайней ячейке
							<<<<<<<
							!правая верхнаяя ячейка матрицы; нужен особый обход матрицы!
							[-]+++ убить клетку
							>>>>>>>[-] выход из else
						]<<<<<<<
						>>>>>>>[-]<<<<<<< сбрасываем флаг else
						>>>>>>>>>>>>[-] возвращаемся в $12; выходим из if
					]<<<<<<<<<<<<
					>>>>>>>[ else; в $12 ноль; крайняя левая ячейка
						<<<<<<< возвращаемся в $0
						!левая верхняя ячейка; нужен особый обход матрицы!
						[-]+++ убить клетку
						>>>>>>>[-] выход из else
					]<<<<<<<
					>>>>>>>>>>>>[-]<<<<<<<<<<<<
					>>>>>>>[-] выход из else
				]<<<<<<< возвращаемся к $0
				>>-
			]<-
		]
		!сдвинуть блок данных в начало; оживить оживших и убить умерших
		>>[>>+<<-]>>[<<+<<+>>>>-]<<<< копируем высоту матрицы
		[ возвращаемся к началу матрицы
			>>>[>+<-]>[<+<<+>>>-]<<<< копируем; сколько ячеек в каждой строке
			>[
				<<[>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-] сдвиг ячейки матрицы и блока
				<<<<<<<<<<<<<- возвращаемся к счетчику
				<< вернулись к значению; нужно проверить; если клетка изменяет свое состояние; надо изменить его
				>>>>>>>>>>>>>>>> перешли к ячейке со значением
				<<[-]<[-]>>>[<<+<+>>>-]<<<[>>>+<<<-]>>> скопировали в предыдущую ячейку
				<< <+> переходим в ячейку с копией значения; флаг else перед ней
				-[ было значение больше единицы
					-[ if; значение было больше 2
						-[ if; значение было 4
							>>[-]+<<
							<[-]>[-] выход из if и else
						]
						<[ else; значение было 3
							- выход из else
						]>
						<[-]>[-] выход из if и else
					]
					<[ else; значение было 2
						> >>[-]+++<< <[-] убиваем клетку; выходим из if
					]>
					<[-]>[-] выход из else и if
				]
				<[ else; значение равнялось единице; клетка не изменяется
					-
				]>
				>>
				<<<<<<<<<<<<<<<<
				!оживить оживших; убить умерших!
				>>
			]<-
		]
		>>[>>+<<-]>>[<<+<<+>>>>-]<<<< копируем в ячейку $1 высоту
		[ печать поколения
			>>>[>+<-]>[<+<<+>>>-]<<<< копируем; сколько ячеек в каждой строке
			>[
				>>>>>>>>>>>>[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-]<[>+<-] с первой по четырнадцатую ячейки блока сдвинуты вправо; текущая ячейка первая
				>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>-]<<<<<<<<<<<<<<< сдвинули ячейку; стоящую после блока данных
				++++++++++++++++++++++++++++++++++++++++++.------------------------------------------ выводим текущую ячейку
				>>-
			]<-
			>>>>>>>>[-]++++++++++.[-]<<<<<<<< печатаем перевод строки
		] поколение выведено на экран; необходимо вернуть блок данных в начало; текущая ячейка первая
		>>[>>+<<-]>>[<<+<<+>>>>-]<<<< копируем высоту матрицы
		[ возвращаемся к началу матрицы
			>>>[>+<-]>[<+<<+>>>-]<<<< копируем; сколько ячеек в каждой строке
			>[
				<<[>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-]>[<+>-] сдвиг ячейки матрицы и блока
				<<<<<<<<<<<<<- возвращаемся к счетчику
			]<-
		]
		>>>>>> переходим к ячейке с флагом
		>>,<< считываем следующий символ
		[-] выход из else
	]>>
]
[-]>[-]>[-]>[-]>[-]<<<<<++++++++++[>+++++++>++++++++++++>++++++++++>++++++++++++>+<<<<<-]>-.>.>+++++.>----.>. Exit
